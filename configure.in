AC_INIT(navi2ch.el)
AM_INIT_AUTOMAKE(navi2ch, 1.5.1-DEV)
AM_MAINTAINER_MODE

# Emacs set environment variable EMACS as t
test "x$EMACS" = xt && EMACS=

emacsen="emacs xemacs"

AC_CANONICAL_HOST

case $host_os in
*cygwin*)
  with_bash=yes
  emacsen="$emacsen MeadowNT.exe Meadow95.exe"
  ;;
esac

AC_ARG_WITH(bash,
[  --with-bash             Use Bash to build],
[case $withval in
yes) SHELL=bash ;;
no)  ;;
*)   SHELL="$withval" ;;
esac])

AC_ARG_WITH(xemacs,
[  --with-xemacs           Use XEmacs to build],
[case $withval in
yes) emacsen="xemacs" ;;
no)  emacsen="emacs" ;;
*)   EMACS="$withval" ;;
esac])

AC_ARG_WITH(emacs,
[  --with-emacs            Use Emacs to build],
[case $withval in
yes) emacsen="emacs" ;;
no)  emacsen="xemacs" ;;
*)   EMACS="$withval" ;;
esac])

if test -n "${with_xemacs+set}" && test -n "${with_emacs+set}"; then
  AC_MSG_ERROR(specify one of --with-xemacs or --with-emacs)
fi

AC_CHECK_PROGS(EMACS, ${emacsen}, no)
if test $EMACS = "no"; then
  AC_MSG_ERROR(cannot find emacs)
fi

AM_PATH_LISPDIR

AC_ARG_WITH(icondir,
[  --with-icondir=DIR      Override default icons directory],
[ icondir="$withval"
AC_MSG_CHECKING([where icon files should go])
AC_MSG_RESULT([$icondir])],
[
if test -z "${icondir}"; then
  AC_CACHE_CHECK([where icon files should go], [cv_icondir], [dnl
    cv_icondir=`$EMACS -batch -q -eval '(princ (expand-file-name "navi2ch/icons" data-directory))'`
    if test -z "$cv_icondir"; then
      cv_icondir='${datadir}/pixmaps/navi2ch'
    fi
  ])
  icondir="$cv_icondir"
fi
])

AC_SUBST(icondir)

AC_OUTPUT([Makefile contrib/Makefile doc/Makefile icons/Makefile])
